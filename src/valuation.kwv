/**
 * Valuation (a better name would be "value" but it conflicts with the `value of` k.IM operator) articulates notions
 * relative to value, its attribution, and the related notions of asset and stake, risk, safety, vulnerability. It 
 * sits in the philosophical realm of _axiology_, the branch of philosophy that studies the nature of value, including 
 * ethical and aesthetic value as well as economic, health and others.
 */
ontology valuation
	in domain imod:Physical of imod:Anthroposphere
	version 1.0
	using imod, physical, decision;

/**
 * The @Asset role is, for now, limited to substantials (individuals, organizations, systems, agents) but could be
 * extended to configurations and maybe processes. A @Stakeholder implies the role @Asset for
 * something, so that when the observer is a @Stakeholder all observable @Assets will be
 * resolved. Specific stakeholders may specialize the assets they imply.
 */	
role Asset 
	"The asset role is conferred to substantials that are valued more than zero."
	marks value of (imod:Subject or imod:Agent);

/**
 * The following are roles for quantities that are created by the correspondent emerging 
 * configurations. The individual hazard and other subclasses must confer the specialized
 * roles so that they can be resolved automatically when the configuration emerges.
 */
abstract role ValueMetric
	is imod:Indicator
	increases with probability of physical:Collapse of valuation:Asset
	has children
		VulnerabilityMetric,
		ExposureMetric,
		RiskMetric;
	
/**
 * An observer or context that incarnates @Stakeholder will cause the resolution of @Asset for any 
 * compatible observation made. In turn, any assets found will engender the relative configurations.
 * Only @{imod:Agent}s can be @{Stakeholder}s.
 */
role Stakeholder
	"An agent with a stake in something recognized as an asset within its observational scope."
	implies valuation:Asset within imod:Subject;
	
role Hazard
	"An occurrent that affects a valued asset."
	applies to (imod:Process or imod:Event)
	implies probability of physical:Collapse of valuation:Asset;
	
abstract configuration Vulnerability
	"The existence of assets that can be affected by hazards."
	emerges from valuation:Asset within imod:Subject
	implies valuation:VulnerabilityMetric;
	
/**
 * Exposure is engendered when an @Asset coexists with a compatible @Hazard (the compatibility is implied by the 
 * `with` operator). 
 */
abstract configuration Exposure
	"The compresence of assets with hazards that affect them."
	emerges from (valuation:Asset with valuation:Hazard) within imod:Subject
	creates ExposureMetric;
	
abstract configuration Risk
	"The compresence of exposure, implying assets threatened by hazards, with the vulnerability of the same assets."
	emerges from (valuation:Exposure with valuation:Vulnerability) within imod:Subject
	creates RiskMetric;

	