/**
 * "Literate programming" features: comments introduced with the double asterisk are collected to provide
 * AsciiDoc documentation, and the concepts that follow them are documented in the processed document. If the
 * namespace is not introduced by a literate section, it won't be documented at all. 
 * 
 * @Section headers are turned into #/## etc., using intelligent nesting depending on the context (so they can start at
 * one @ unless a subsection of the previous at the same level is wanted). All the remaining text in the comment is 
 * standard Markdown or AsciiDoc, added verbatim except for a possible leading asterisk that is always stripped (along with
 * the first space after it, if present).
 * 
 * In the text, the prefix @ is used to expand the next word intelligently - for example adding a cross-reference when
 * it's the recognizable and unambiguous URN of any k.LAB asset. In case it isn't understood, the preprocessor will
 * print a warning and insert the word without the @, unless it's escaped with another @.
 * 
 * k.IM code prefixed by literate sections is inserted in the document where it belongs, with syntax highlighting and line numbering
 * dependent on processing options.
 * 
 * Re: @imod (must become the intro here):
 * 
 * The epistemological framework for @im is systems thinking. The domain structure defines how that is articulated within the
 * worldview. The domains are contextualized to explicit systems, introduced in general here. TODO should be compared quickly
 * with other possible choices: e.g. rationalism, realism (BFO), pragmatism, empiricism, constructivism.
 * 
 * Each domain is a perspective on a system. The two main perspectives are the ontological (Physical domain) and the epistemological
 * (Knowledge domain). The systems are explicit in this worldview, expressed as Configurations ("perceived" systems). The physical
 * domain pertaining to a system is one and one only. The knowledge domains may be multiple, according to what the knowledge
 * described in each domain emphasizes (e.g. @economics, @sociology and @policy for the @society domain). Of course the 
 * ontological domains are _also_ knowledge - but from within the worldview, they are assumed to represent the ontological
 * reality of the observed world and are assumed complete and internally consistent, while there is no such requirement for individual knowledge
 * domains. 
 * 
 * Tier 1 ontologies *must* declare their domain, which locates them within a system and a perspective on it. Tier 2 domains
 * *must not* declare the domain, as they inherit them from the corresponding Tier 1 ontology, which must exist. This project
 * contains only Tier 1 ontologies, which have a single name as namespace without dot separators.
 * 
 * E.g., a definition for the @agency space: Systems thinking provides a framework for understanding the complexity of decision processes and their 
 * consequences within a holistic context. It emphasizes the interconnections between parts of a system and how those parts 
 * influence one another. Within this framework, a decision process is seen as part of a larger system of actions and 
 * feedback loops, with consequences understood in terms of their impact on the system as a whole. 
 * 
 * Also this (and the Physical domains) is a good place to discuss what @im is not covering and why.
 */
namespace imod
   "The imod (Integrated Modelling) ontology includes abstract concepts related to scientific observations.
    It specifies semantics used everywhere in the IM worldview and links im-based concepts to the 
    fundamental ontology for use in derived products."
   version 1.0
   in domain root 
      with core 
          // TODO switch to this import after deciding how to link derived observables created by operators 
          // TODO add the klab: namespace to ground the geometries and other characteristics. Probably the
          //      object properties should also go there, although I still don't know how to tie them here.
         "https://raw.githubusercontent.com/integratedmodelling/odo-im/master/releases/0.1.0/odo.owl" as odo
   metadata {
      dc.creator: (
         "Ferdinando Villa (ferdinando.villa@bc3research.org)"
         "Greta Adamo (greta.adamo@bc3research.org)"
      )
      // TODO add refs and links
   };


/**
 * @ The main Systems of the earth
 * 
 * This worldview takes a _system view_ of planet Earth and characterizes its main ontologies in domains relative to 
 * explicit systems. The @System hierarchy articulates the basic systems taken into consideration in the @im worldview. 
 * 
 * Each of the @imod domains is either an ontological (Physical, i.e. a take on the system itself and its physical components) 
 * or epistemological (Knowledge, i.e. the related disciplines, quantities, indicators and scientific understanding relative 
 * to the system) take on a specified system.
 * 
 * We interpret systems as an observationally recognizable configuration of interconnected parts. The system is expressed in each
 * domain ontology through the inherency in the domain concept that characterizes it. The root @EarthSystem is concrete because
 * domains like @physics and @chemistry apply to the entirety of it, including all its subclasses.
 * 
 */
configuration EarthSystem 
	"Any interpretation of the Earth that we conceive as a self-consistent system in it. Used as
     an element of discourse to contextualize the ontological and epistemological domains of the
     @im worldview."
	has children 
		/**
		 * Description: The @Atmosphere encompasses the layers of gases surrounding the Earth. It is responsible
		 * for climate regulation, weather patterns, and the distribution of temperature and precipitation across 
		 * the globe. The atmospheric system plays a critical role in supporting life by providing essential gases 
		 * like oxygen and carbon dioxide and protecting life from harmful solar radiation.
		 * 
		 * Interactions: Interacts with the hydrosphere through the water cycle and with the biosphere through 
		 * the exchange of gases essential for life.
		 */
		Atmosphere
		"",
		/**
		 * Description: The @Hydrosphere includes all of Earth's water, covering oceans, seas, lakes, rivers, 
		 * groundwater, and ice (glaciers and polar ice caps). It is crucial for regulating temperature, supporting 
		 * aquatic life, and providing fresh water for terrestrial life forms.
		 * 
		 * Interactions: Influences and is influenced by the atmospheric system (evaporation and precipitation), 
		 * the geosphere (shaping landforms through erosion), and the biosphere (providing habitats).		 */
		Hydrosphere
		"",
		/**
		 * Description: The @Geosphere refers to the solid Earth, from the ground we walk on to the Earth's core. 
		 * It encompasses mountains, plains, sea floors, and all other landforms, as well as the processes that 
		 * create and modify these landforms, such as tectonic movements, volcanism, and erosion.
		 * 
		 * Interactions: Shapes the biosphere by creating habitats, interacts with the atmosphere through volcanic 
		 * eruptions, and affects the hydrosphere by determining the flow of surface and groundwater.
		 */
		Geosphere,
		/**
		 * Description: The @Biosphere is the sum of all ecosystems on Earth, including all living organisms and the environments in which they live. It spans from the deepest ocean floors to the highest mountain peaks and includes complex interactions among organisms and between organisms and their physical environments.
		 * Interactions: Interacts with all other Earth systems by altering the atmosphere (through photosynthesis and respiration), influencing the hydrosphere (impacting water quality and availability), and modifying the geosphere (through processes like soil formation and erosion control).
		 */
		Biosphere,
		/**
		 * Description: The @Anthroposphere (or technosphere) represents the part of the environment made or modified by humans for use in human activities and habitation. It includes cities, infrastructure, and technology.
		 * Interactions: Has significant impacts on and interacts with all other Earth systems through urbanization, pollution, land use changes, and climate change.
		 */	
		Anthroposphere,
		/**
		 * We separate the technosphere from the anthroposphere to facilitate the articulation of physical domains re: human societies and
		 * their artifacts. This separation is justified by widely different time scales of description, inherent processes, ethical rules and 
		 * representational conventions for them.
		 */
		Technosphere;

 /**
 * @Domains
 * 
 * Domains are philosophical takes on @{System}s, either in an ontological sense (defining their "nature") or in an epistemological
 * sense (explaining their characteristics through scientific quantities, process descriptions, etc). Each domain is associated
 * to a specific system type articulated here as the basic worldview for the @im project.
 * 
 * The relationship of the basic scale extents of time and space should be articulated and specialized within the description of
 * each system. Also the "conceptual" extent could be added to epistemological (Knowledge) domains if it's decided to use it 
 * to articulate competing explanations.
 * 
 * When used in the domain specification of the worldview's namespaces, the domains should be contextualized to the systems
 * or subset thereof that they address. This would be grounds for validation of each domain as the contents should be aligned
 * with that system and provide its full description (specializing further in each tier). 
 */
 
/**
 * @@ The Physical domain is the ontological perspective on a system. All Physical domains are ontological statements 
 * and only one physical domain per system type is permitted in the @im worldview.
 * 
 * @todo knowledge domains like @physics and @chemistry should apply to the general PhysicalSystem and EarthSystem should
 * be the focus (root?) domain for the ontology.
 */
abstract domain Physical
	"The basic ontology domain for things that are conceptualized as existent. Namespaces in this domain have to do
     with the world, not with its epistemological explanation or interpretation."
     applies to System;

/**
 * @@ Knowledge domains are epistemological takes on a system. Different disciplines may coexist in the attempt to
 * explain the system, therefore more than one knowledge domain can exist on each system type. 
 */
domain Knowledge
	"The basic epistemology domain. Has to do with explanations and is always connected to a physical domain."
	applies to System;

/*
 * TODO there should be a mechanism to link all fundamental types to the core ontology and validate
 * coverage and consistency. For now the mechanism lacks completeness and validation.
 * 
 * For this to work, we also need to link the output of unary operators. This could be as simple as
 * 
 * abstract quality Numerosity is core count of imod:Quality;
 * abstract event Change is change in imod:Quality
 * 
 * TODO we should either have all core derivation use 'equals' or 'is' mandatorily. Given that we will
 * likely supplement the core namespace with properties, probably 'is' is better so that all derivations
 * are in the same place. Equalities should be reserved for in-worldview statements.
 */ 

// TODO link the output of operators. Could be as simple as

// abstract quality Numerosity is core count of imod:Quality;
// abstract event Changed is changed imod:Quality

// would need a check that everything is included and store those in CoreOntology for reference. 
// Not sure what we should do for restrictions but probably just adding the properties based on the operators would be enough.           
// Also all needed annotation properties would be just added to the core namespace and that would be it. 
 
//Identity
abstract identity Identity
    "Identity specifies the assignment of a concept to a certain class of concepts based on 
     a series of characteristics, such as morphological, behavioral and contextual"
   is core odo:Identity;
   
// Identities capturing discrete time for the worldview
// TODO MOVE THESE TO THE klab: NAMESPACE
//identity Hourly is core observation:Hourly;
//identity Yearly is core observation:Yearly;
//identity Monthly is core observation:Monthly;
//identity Daily is core observation:Daily;
//identity Weekly is core observation:Weekly;


//Identities capturing discrete space for the worldview

//abstract identity Puntal is core observation:Puntal;
//abstract identity Lineal is core observation:Lineal;
//abstract identity Areal is core observation:Areal;
//abstract identity Volumetric is core observation:Volumetric;
 
//@fixme("Mah. A final output would be Outcome, Goal is definitely not an identity, probably both are configurations") 
abstract identity Outcome
     "Final output of a Process";

//The notion of Boundary should be better documented

@fixme("The 'requires identity' clause needs revision, here and elsewhere. Implication (to be understood) is a likely candidate.")
abstract identity ConceptualBoundary 
     //metadata { plato.stanford.edu/entries/boundary/ }
    "A conceptual boundary is the conceptual delimitation that allows the identification of entities"
     requires identity Goal;
  
     
// State of affair: https://plato.stanford.edu/entries/states-of-affairs/#:~:text=States%20of%20affairs%20are%20similar,explained%20further)%20objects%20and%20properties.
//abstract identity StateOfAffairs
//     "State of affairs is a complex situation that obtain or not"
//     has children
 //    Obtainable,
  //   Unobtainable;

//Attribute
   
abstract attribute Attribute
    "Attribute specifies descriptive accidental predicates of a subject."
   is core odo:Attribute;
   

abstract attribute Evidence
     "Characterizes degrees of evidence for an hypothesis."   
     has children 
         Possible "Hypothesis cannot be dismissed based on reasoning although no documented supporting observations exist.",
         Certain "No doubt can remain about evidence - self-evident or blatant truth",
         Hypothetical "Hypothesis can be made in support of an interpretation although no documented supporting observations exist. Similarly valid counter-arguments may exist.",
         Observed "Degree of evidence is supported by documented observations, but still  evidence is indirect.";


abstract subjective ordering Coarseness 
   has children
      Fine,
      Coarse;
   
abstract subjective ordering Level 
      "Two-level hierarchy for generic discretizations." 
      has children  
      (Low 
            has children 
              Minimal, 
              ExtremelyLow, 
              VeryLow, 
              ModeratelyLow
             ),
        Moderate, 
       (High 
            has children
              ModeratelyHigh,
              VeryHigh,
              ExtremelyHigh,
              Maximal
            );
   
//Realm
   
abstract realm Realm
    "Realm specifies the physical (e.g. of the biosphere) location of a concept"
   is core odo:Realm;

//Role

abstract role Role
    "Role specifies predicates that can be played by a concept in a context"
   is core odo:Role;
   
deniable attribute Comparable
   "Describes the ability to rank an object over another one."
   applies to imod:Concordance;

role Reference
    "Applied to an observation used as reference to compute the primary observation by comparison. 
     Can be linked to a rescaling filters to shift the context of observation made in different space and time."
    requires attribute Comparable;

@todo("check extraction of attributes")
role Indicator 
   applies to Quantity
   has children   
     (AbsoluteIndicator
     "An absolute indicator has values that range between a known minimum and maximum. Its value corresponds 
      to a state that is fully understood in terms of the indicator."  
       ),
     (RelativeIndicator 
     "A relative indicator can only compare two states of the same entity within the same context of observation.
      It does not suggest any absolute value for the state it refers to."
     );

/**
 * Observable
 */

//Process
abstract process Process
     "A dynamic occurrence unfolding over time, typically open-ended (no temporal boundaries) and homogeneous in nature."
   is core odo:Process;
    
//Event
     
abstract event Event
    "An event is an independent occurrent that happens in time, close-ended (with temporal boundaries) and with participants."
   is core odo:Event;
       
@todo("should have some articulation of the 'Outcome' part")
@todo("see note above for the 'requires identity' clause")
deniable attribute Positive
    "Describes the positive or negative outcome of an event or a subject."
    requires identity Outcome;

@todo("Must articulate relationship vs. bond and functional vs. structural, as these are fundamental and disjoint.")   
abstract relationship Relationship
      "A relationship directionally connects two subjects or agents."
    is core odo:Relationship
    has disjoint children
        (StructuralRelationship
        	"Structural relationships between two substantials (relata) allow another substantial (relator) to emerge."
        	is core odo:StructuralRelationship
        	),
        (FunctionalRelationship
        	"Functional relationships between two substantials (relata) allow an event (relator) to emerge."
        	is core odo:FunctionalRelationship
        	),
        (Bond
        "Bi-directional relationship between substantials"	
        )
    ;

abstract configuration Configuration
    "Configurations represent emergent patterns (aggregates of other entities) that are generated by observations of 
     quality and relationship."
   is core odo:Configuration;



abstract configuration Network 
   "A network is a configuration of subjects connected by relationships."
   is imod:Configuration of imod:Relationship
   has children 
      (abstract SpatialNetwork  
        //inherits Areal
      ),
      (
      abstract IntentionalNetwork
      inherits Outcome  
      );

/**
 * Countable
 */

@fixme("Not sure this should be articulated here. Seems to me that Substantial is more generic than agent.")
abstract thing Substantial
   "Substantial is independent for its existence and bears qualities. Substantial are similar to Substantial 
    in UFO and DOLCE" 
   is core odo:Substantial
     has children
      ( Subject
      	"Subject is a Substantial without agentive characteristics"
      	is core odo:Subject
      ),
       (Agent
      	"Agent is a Substantial with intentionality" 
      	is core odo:Agent
      );

//abstract thing Subject
//    "Subject is a Substantial without agentive characteristics"
//   is core odo:Subject;

//@fixme("Apart from the circular reference, not sure having agency means the same as 'being Intentional'.")
//abstract agent Agent
//   "Agent is a Substantial with intentionality" 
//   is core odo:Agent
   // FIXME circular dependency (and wrong)
//   inherits decision:Intentional
 //  ;
   
 


/**
 * Qualities
 */
 
abstract quality Quality
     "Qualities represent observables that cannot be observed directly but only through other entities that bear them.
      Their observation produces 'values' in some reference domain. They inhere to other entities and depend on their bearers."
     is core odo:Quality;

abstract quantity Quantity
    "Quantity are qualities whose values are quantified numerically."     
   is core odo:QuantifiableQuality;
   
abstract duration Duration 
   is core odo:Duration;
   

//Space qualities
//Comment: re-think "observation" ontology and below concepts
@fixme("Decide what to do with the spatial geometry identities. They can be here w/o referring to ODO (probably best) although
        they should refer to klab: namespace as they're needed in the software.")
abstract area Area 
   is core odo:Area 
   requires identity (imod:Areal or imod:Volumetric);

abstract length Length
   is core odo:Length 
   requires identity (imod:Lineal or imod:Areal or imod:Volumetric);       

//Artifact capability inspired by: Borgo, S., Vieu, L.: Artefacts in formal ontology. In: Philosophy of technology and engineering sciences
abstract attribute Capability
     "Capability is the described quality of certain artifacts and natural agents."
     requires identity Goal
     describes Quality
     has disjoint children
        ArtifactCapability,
        NaturalCapability;
        
//abstract attribute AttributedCapability
//     "Attributed capability refers to capabilities of artifacts and natural agents assigned by other agent."
//     applies to physical:Artifact, physical:HumanMadeResource, Agent;


//Numerosity

abstract quantity Numerosity     
   is core odo:Numerosity;


/**
 * Intensive physical properties 
 * TODO will make definitions mandatory at some point :)
 */
 

abstract angle Angle 
   "Geometric property that measures the amount of rotation needed to bring one line or plane into coincidence with another. 
   Typically measured in degrees or radians."   
   is core odo:Angle;

abstract energy Energy
   "The capacity of a system to perform work or produce heat. Typically measured in joules (J)." 
   is core odo:Energy;

abstract temperature Temperature
   "Measure of the average kinetic energy of the particles in a substance or system. 
   Typically measured with scales such as Celsius, Fahrenheit, or Kelvin."
   is core odo:Temperature;

resistivity Resistivity 
   "A measure of a material's resistance to the flow of electric current. 
   It quantifies how strongly a material opposes the flow of electrical charges and is typically measured in ohm-meters."
   is core odo:Resistivity;

abstract velocity Velocity
   "Quantity that describes the rate of change of an object's position with respect to time. 
   Typically measured in units such as meters per second (m/s) or kilometers per hour (km/h)." 
   is core odo:Speed;


abstract mass Mass
   "Property of matter that quantifies the amount of substance in an object. 
    Typically measured in units such as kilograms (kg) or grams (g)."
   is core odo:Mass 
   requires identity Volumetric;

abstract amount Amount
   "Refers to a quantitative measure or quantity of something. 
    It represents the magnitude or volume of a substance, object, or phenomenon, and can be expressed numerically or qualitatively."
   is core odo:Amount
   requires identity Volumetric;

abstract volume Volume
   "Measure of the three-dimensional space occupied by an object or substance. It quantifies the amount of space enclosed by the boundaries 
   of the object or substance and is typically measured in cubic units such as cubic meters or cubic centimeters." 
   is core odo:Volume
   requires identity Volumetric;
   
abstract weight Weight
   "The force exerted on an object due to gravity. 
   Typically expressed in units of force such as newtons (N) or pounds-force (lbf)."  
   is core odo:Weight  
   requires identity Volumetric;
   
abstract quantity Proportion
   "The relative size, quantity, or magnitude of one element compared to another within a given context. 
   Can be expressed numerically, often as a fraction, percentage, or ratio."       
   is core odo:Proportion;

abstract quantity Ratio
   "The quantitative relationship between two quantities or amounts, typically expressed as the quotient of one quantity divided by another. It indicates the proportion or comparative magnitude of one quantity relative to another. 
    Ratios can be represented in various forms, such as fractions, decimals, or percentages."       
   is core odo:Ratio;


//Category
   
abstract class Type 
    "Type defines categories"        
   is core odo:EnumerableQuality; 

//Priority

abstract priority Priority
   "Relative importance or precedence assigned to entities or actions within a system or context, often based on predefined criteria or constraints." 
   is core odo:Priority;

abstract priority Concordance
"Conceptual distance from an idea or theory"
;

//Value

abstract money MonetaryValue
    "Quantitative representation of the economic worth or exchangeable value assigned to goods, services, or assets, typically denoted in a currency unit."
    is core odo:MonetaryValue;
    
abstract quantity PreferenceValue
   "Quantitative or qualitative measure representing the degree of desirability, favorability, or priority assigned to an option, choice, or outcome within a decision-making context."
   is core odo:PreferenceValue;
   
abstract role ValueObject
     "An entity that embodies, represents, or holds value, which may include but is not limited to monetary, conceptual, or symbolic value."
     applies to Subject;
     
abstract relationship ValuationRelationship
     "Connection or association between entities or concepts that involves the assessment, determination, or assignment of value."
     links Agent to Subject;

deniable attribute Regularity
	"Denotes the state or quality of being consistent, orderly, or conforming to a pattern or set of rules, often implying predictability and absence of deviation or randomness."
     has disjoint children
         (Regular
         	"State or condition characterized by consistency, orderliness, or adherence to established patterns or rules."
         ),
         (Irregular
         	" Lack of orderliness, or deviation from established patterns or rules, often exhibiting unpredictability or variability in behaviour or occurrence."
         );
         
//Extent

//extent Multiplicity 
//    is core odo:Multiplicity;

@fixme("definitely needs better derivation. For these dichotomic articulations we could use 'sealed' but we could also have a 
        deniable attribute for the positive case and add an alias for the negative: e.g.
        deniable attribute Net; attribute Gross equals not imod:Net;")   
abstract attribute MeasurementRelated
      has children
      (@colormap(colors=(red white green), midpoint=0)
      Net
      ), 
      Gross;
      
//@colormap(colors=(red white green), midpoint=0)      
//attribute Net
//	"" 
//	deniable as Gross;
     
//Temporal extent
            
abstract extent Time 
     is core odo:Time 
     has disjoint children 
     (RegularTime 
         "Regular time is observed as a grid of equal contiguous durations." 
          inherits imod:Regular
     ),
     (IrregularTime 
      "Irregular time is observed as contiguous sequence of intervals of possibly different duration."
     inherits imod:Irregular
     );
    
//Spatial extent
 
 abstract extent Space 
    "Space in the IM worldview is 0, 1,2 or 3-dimensional, with implied traits that describe human perception and modalities of description proper of an Euclidean view of the world."
    is core odo:Space
    has disjoint children ( 
       PuntalSpace 
       inherits Puntal
       ),
       (LinealSpace
         inherits Lineal
       ),
       (abstract ArealSpace 
           inherits Areal 
           has children 
               (GridSpace  
                  inherits Regular 
                ),
                (VectorSpace
                inherits Irregular  
                )
       ),
       (abstract VolumetricSpace
         inherits Volumetric
             has children
                  (VoxelSpace
                  inherits Regular  
                  ),
                  (PolyedricSpace
                  inherits Irregular   
                  )
            );
             


  