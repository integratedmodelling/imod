namespace physical
   using imod,decision;
   
//Physical entities, e.g. subjects, events, processes, qualities

event Collapse
   "The loss of identity for the system, after which it ceases to exist as such. In  living systems, it's synonymous with death."
    applies to imod:System
;

abstract thing Artifact
    "Artifact is intentionally created by an agent for a goal"
    is decision:Intentional imod:Created imod:Subject
    requires identity imod:Goal;

//Adamo, G., and Willis, M. (2022, May). Conceptual Integration for Social-Ecological Systems: An Ontological Approach. RCIS 2022
abstract role Resource
     "A resource is the role that objects plays in the context of activities or plans to achieve goals"
     is decision:Relevance imod:Role for imod:Goal
     has children
        (NaturalResource),
        (HumanMadeResource
        "HumanMade resources are the equivalent of artifacts."
        applies to Artifact
        );
        
        
//Comment: I am aware that I am using "with" in unconventional manner. To be revised.    
//From DOLCE.
abstract thing Feature
   "Feature is (cognitive) relevant subject that are dependent upon their host, such as holes and bumps."
   is imod:Subject with decision:Relevance
   inherits imod:AttributedCapability
   has children
       Hole,
       Slot,
       Bump;
   
// From: DOLCE Lite Plus (DLP). E. M. Sanfilippo: "Ontologies for information entities: State of the art and open challenges"
abstract thing InformationRealization
     "Information realization is the physical support of an information object"
     is Artifact with imod:Evidence     
     has children
        (Document
            has children
                (Specification),
                (PlanRealization),
                (Book),
                (Report)
                ),
                
        (ComputerProgram);
        
//Specialized physical qualities
abstract length Depth
    is imod:Length
    requires identity imod:Volumetric
;

abstract length Height
     is imod:Length
     inherits physical:Vertical
     requires identity (imod:Areal or imod:Volumetric)
;

abstract length Width
    is imod:Length
    inherits Horizontal
    requires identity (imod:Areal or imod:Volumetric);
 
    	
abstract area SpatialCoverage is imod:Area;

abstract quality Acceleration
	is change rate of imod:Velocity;
	
quantity Impedance
	"The most general quality that describes resistance to a physical force, flow or 
     other phenomenon. Usually given the symbol Z in technical literature."
	// TODO children - electrical has a core representation
	;

quantity Conductance
	"The opposite of impedance, describing the effectiveness of transport of an observable through a medium or a region.."
	// TODO children - fluid conductance, electrical conductance, thermal conductivity
;

abstract duration Age
      is imod:Duration;
      

abstract quality Frequency
	"Frequency is the numerosity of events in a time period. Used in continuous time representations to 
    describe a wave."
	is count of imod:Event;

abstract duration Period
	"The time between two repetitions of an event. If the events are described by a quality in continuous time, it 
     describes the distance between high points on a wave function. Inversely proportional to frequency."
	decreases with Frequency;

abstract volume Capacity;

abstract angle Direction;

//Definition of diversity: Xu, S., Bottcher, L., and Chou, T. (2020). Diversity in biology: definitions, quantification and models. Physical Biology, 17(3), 031001.
abstract quantity Diversity
"Diversity is the quantity of the range of characteristics found in subjects and agents."; 

abstract quantity Heterogeneity equals Diversity;

//Specialized processes
abstract identity TransitiveProcess 
	"Transitive processes happen in a subject directly affecting other observables (their objects)"
	has children
		(
			@example("Transportation: you transport 'something' - the object - modifying its spatial extent.")
			abstract SpatialTransitiveProcess
				"A spatial transitive process affects the spatial extent of the object."
				has children
			        (abstract Transport	
			          )
			);
	
abstract identity IntransitiveProcess 
	"Intransitive processes happen in subjects only affecting the context itself"
	has children
		(abstract SpatialIntransitiveProcess
			"A spatial transitive process affects the spatial extent of the context - e.g. movement."
			has children
				(Movement
					""
				)
		);
	
abstract process Transformation
    "Any endogenous transformation happening in a system and affecting its internal structure."
    inherits IntransitiveProcess
    applies to imod:System
    has children 
		(Coupling
			"Building of interactions. Can affect the mereological identity of the system by creating groups."
		),
		(Decoupling
			"Destruction of interactions. Can affect the mereological identity of the system by destroying groups."
			has children 
				(Dissipation
					"Proceeds through the collapse of parts of the system."
				)
		),
		(Displacement 
			"Akin to movement but causing the system to change because of that"
		),
		(Growth
			 "Positive or negative growth makes a system acquire a larger size or functional throughput."
		),
		(Homeostasis
			"The active maintenance of the current status and/or behavior of the system"
		)
;

       
abstract process Activity
    "An activity is a process performed by an agent"
    is decision:Intentional imod:Process;
    
abstract role ValueActivity
     applies to Activity;
    
abstract process Participation
      is Activity
      applies to Participant
      has children
       Active,
       Passive;
       
//Comment: Change as event
 //from GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Change	
// subclasses from Case file items of CMMN: http://cmmn.byethost4.com/case_file_item_standard_events.html?i=1
       
abstract event Change 
      "Event that specifies qualitative modifications over time. They are important  in dynamic models."       
      has children
         Changed,
         Replaced,
         Updated,
         Deleted;
         
abstract event Input
    "Input is an event that triggers another event or process."
    is Change;

abstract event Output
    "Output is an event generated from another event or process."
    is Change;
    
process Creation
    "Creation is the intentional activity of developing something for a purpose"
    is decision:Intentional Activity
    requires identity imod:Goal;
    
abstract attribute Created
      "Describes the state in which something is created by an agent"
      applies to imod:Subject
      requires identity imod:Goal;   

	
//Specialized physical predicates
abstract attribute MovementRelated
	"MovementRelated describe the type of velocity of a process"
	describes imod:Velocity of imod:Process
	has disjoint children
		Moving,
		Still;
		
abstract subjective ordering HeightLenghtLevel
     applies to /*(Areal or */ imod:Volumetric /*) */ imod:Subject 
     has children   
     (abstract VerticalHeightLevel 
     	applies to imod:Height 
     	has children
     	   TallHeight,
     	   SmallHeight
     	),
    (abstract HorizontalLenghtLevel
    	applies to imod:Lenght
    	has children
    	    Long,
    	    Short  	
    );
    
abstract attribute SpatialOrientation 
			has children
				(abstract ObserverRelativeOrientation 
					has disjoint children
						Right,
						Left,
						Front,
						Back,
						Up,
						Down	
				),
				(abstract EuclidianRelativeOrientation2D 
					"These concepts are purely 2d - nothing in English to denote the 'applicate'
					 third dimension of a 3D space."
					has disjoint children
						Vertical,
						Horizontal
				)
		;

abstract subjective ordering VolumeLevel
      describes imod:Volume 
      applies to imod:Volumetric, imod:Subject
      has children  
         SmallVolume, 
         LargeVolume;

abstract ordering Periodicity
	""
	describes Period
	has children
		(Sporadic 
			""
		),
		(Intermittent 
			""
		),
		(Continuous 
			""
		);
		
abstract attribute TemporalOrientation
			has disjoint children
				Previous,
				Successive;


deniable attribute Perceived
    "Describes a perceivable observable phenomena"
    has children
        Visible,
        Audible,
        Smellable,
        Palpable;

abstract role Throughput 
   "The quantifiable amount of matter, energy or information transferred through a flow connection."
   applies to imod:Output;

abstract role Participant
     "Participants are entities, Subjects or Agents, that participates in Events."
     is imod:Role during imod:Event;

//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Processual_role	
abstract role ProcessualRole
      "ProcessualRole refers to the role played by participants in a process."
      is imod:Role during imod:Process;

//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Relational_role
abstract role RelationalRole
      "RelationalRole refers to the role played by participants in a functional or structural relationship."
      ;

//Boundary literature: Varzi, Achille, "Boundary", The Stanford Encyclopedia of Philosophy (Winter 2015 Edition), Edward N. Zalta (ed.), URL = <https://plato.stanford.edu/archives/win2015/entries/boundary/>.
abstract thing SpatialBoundary 
    "A spatial boundary is the spatial delimitation between entities"
    inherits imod:Areal;
    
 //Boundary literature: Varzi, Achille, "Boundary", The Stanford Encyclopedia of Philosophy (Winter 2015 Edition), Edward N. Zalta (ed.), URL = <https://plato.stanford.edu/archives/win2015/entries/boundary/>.
abstract extent TemporalBoundary 
    "A temporal boundary is the temporal delimitation between entities."
     inherits TimePosition;
     
// time concepts

//Revisitation as attribute and not duration from SUMO v 1.75: https://www.ontologyportal.org/images/SUMOclasses.gif	
abstract attribute TimePosition
    "Time position is a perspective to measure time"
    applies to imod:Duration
    has children
    TimeInterval,
    TimePoint;
    
//Temporal extent
       	   
 abstract extent Time 
     is core observation:Time	
     has disjoint children 
     (RegularTime 
     "Regular time is observed as a grid of equal contiguous durations." 
     inherits imod:Regular),
     (IrregularTime 
      "Irregular time is observed as contiguous sequence of intervals of possibly different duration."
     inherits imod:Irregular
     );
    
    
//Spatial extent
 
 abstract extent Space 
    "Space in the IM worldview is 0, 1,2 or 3-dimensional, with implied traits that describe human perception and modalities of description proper of an Euclidean view of the world."
    is core observation:Space
    has disjoint children ( 
       PuntalSpace 
       inherits imod:Puntal
       ),
       (LinealSpace
       	inherits imod:Lineal
       ),
       (abstract ArealSpace 
       inherits imod:Areal 
           has children 
               (GridSpace 	
               	inherits imod:Regular 
                ),
                (VectorSpace
                inherits imod:Irregular	
                )
                ),
       (abstract VolumetricSpace
       	inherits imod:Volumetric
       	    has children
       	         (VoxelSpace
       	         inherits imod:Regular	
       	         ),
       	         (PolyedricSpace
       	         inherits imod:Irregular	
       	         )
       	   );


