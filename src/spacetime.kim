private namespace spacetime
using imod;

//Discrete space and time for the worldview

abstract identity Puntal is core observation:Puntal;
abstract identity Lineal is core observation:Lineal;
abstract identity Areal is core observation:Areal;
abstract identity Volumetric is core observation:Volumetric;

identity Hourly is core observation:Hourly;
identity Yearly is core observation:Yearly;
identity Monthly is core observation:Monthly;
identity Daily is core observation:Daily;
identity Weekly is core observation:Weekly;

abstract attribute Orientation 
	has children
		(abstract SpatialOrientation
			has children
				(abstract ObserverRelativeOrientation 
					has disjoint children
						Right,
						Left,
						Front,
						Back,
						Up,
						Down	
				),
				(abstract EuclidianRelativeOrientation2D 
					"These concepts are purely 2d - nothing in English to denote the 'applicate'
					 third dimension of a 3D space."
					has disjoint children
						Vertical,
						Horizontal
				)
		),
		(abstract TemporalOrientation
			has disjoint children
				Previous,
				Successive
		);

//This follows the Allen's Interval-based notation: https://plato.stanford.edu/entries/logic-temporal/#PriBasTenLogTL	
abstract attribute TemporalDescription
      "Specifies the temporal information for certain states"
     applies to Event, Process
     describes Period, Time 
     has children 
      Equals,
      Before,
      After,
      Meets,
      Overlaps,
      Starts,
      Finishes,
      TemporalContainment,
      During
;

abstract attribute Change
	"These attributes have to do with the comparison of a value along time. They are important
	 in dynamic models."
    applies to Quantity 
	has disjoint children
		  (Created 
			"Applied to a numeric quantity, it makes it a derivative. Dynamic models use this automatically." 
			),
		  (Updated
			"Describes the positive component of change. Implies quantifiable."
		  applies to Quantity
		  ),
		  (Replaced
			"Describes the positive component of change. Implies quantifiable."
		  applies to Quantity
		  ),
		 (Deleted 
		    "Describes the negative component of change. Implies quantifiable."
		 applies to Quantity
		 )
;

deniable attribute Regularity
     applies to Event, Process
     has disjoint children
         Regular,
         Irregular
         ;
         
abstract subjective ordering HeightLenghtLevel
     applies to /*(Areal or */ Volumetric /*) */ Subject 
     has children   
     (abstract VerticalHeightLevel 
     	describes Height 
     	has children
     	   TallHeight,
     	   SmallHeight
     	),
    (abstract HorizontalLenghtLevel
    	describes Lenght
    	has children
    	    Long,
    	    Short  	
    );
    
abstract subjective ordering VolumeLevel
      describes Volume 
      applies to Volumetric, Subject
      has children  
         SmallVolume, 
         LargeVolume;
         
 //Extent
 
 abstract extent Space 
    "Space in the IM worldview is 0, 1,2 or 3-dimensional, with implied traits that describe human perception and modalities of description proper of an Euclidean view of the world."
    is core observation:Space
    has disjoint children ( 
       PuntalSpace 
       inherits Puntual
       ),
       (LinealSpace
       	inherits Lineal
       ),
       (abstract ArealSpace 
       inherits Areal 
           has children 
               (GridSpace 	
               	inherits Regular 
                ),
                (VectorSpace
                inherits Irregular	
                )
                ),
       (abstract VolumetricSpace
       	inherits Volumetric
       	    has children
       	         (VoxelSpace
       	         inherits Regular	
       	         ),
       	         (PolyedricSpace
       	         inherits Irregular	
       	         )
       	   );
       	   
 abstract extent Time 
     is core observation:Time	
     has disjoint children 
     (RegularTime 
     "Regular time is observed as a grid of equal contiguous durations." 
     inherits Regular),
     (IrregularTime 
      "Irregular time is observed as contiguous sequence of intervals of possibly different duration."
     inherits Irregular
     );
 

