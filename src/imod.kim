namespace imod
	"The imod (Integrated Modelling) ontology specifies semantics used everywhere in the IM worldview
	 and links im-based concepts to the fundamental ontology for use in derived products."
	version 1.0
	in domain root
	metadata {
		dc:creator: (
			"Ferdinando Villa (ferdinando.villa@bc3research.org)"
			"Greta Adamo (greta.adamo@bc3research.org)"
		)
	};

/**
 * IMOD includes abstract concepts related to scientific observations. This is the most general ontology that grounds all the more specific ones.
 */

/**
 * Import classes from ODO-IM core ontology: https://github.com/integratedmodelling/odo-im
 */	

  
//Identity
 
 abstract identity ConceptualBoundary 
     //metadata { plato.stanford.edu/entries/boundary/ }
    "Abstract representation defining the limit or interface between different conceptual entities"
     requires identity Goal;
  
     
// State of affair: https://plato.stanford.edu/entries/states-of-affairs/#:~:text=States%20of%20affairs%20are%20similar,explained%20further)%20objects%20and%20properties.

abstract identity StateOfAffair
	"A complex situation that may or may not be realized."
	has children
	(Obtainable
	"A situation that is achievable or can be realized "	
	),
	(Unobtainable
	"An unrealizable or not achievable situation"
	);

	
abstract attribute Attribute
    "Characteristic or property that describes or represents a feature of entity."
	equals core odo:Attribute;
	

abstract attribute Evidence
     "Represents information or data that supports the existence of a subject or entity."   
     has children 
         (Possible 
         "Plausible information or data that may contribute to the confirmation of the evidence."
         ),
         (Certain 
         "Confirmation of the evidence"
         ),
         (Hypothetical 
         "Speculative information based on assumptions. Similarly valid counter-arguments may exist."
         ),
         (Observed
          "Degree of evidence supported by documented but indirect observations."
          );


// Coarseness: https://theses.hal.science/tel-00327542/document

abstract subjective ordering Coarseness
	"categorization of entities based on the perceived granularity or fineness of their characteristics" 
	has children
		(Fine
		"Detailed or intricate level of granularity."	
		 ),
		(Coarse
		"Broader or less detailed level of granularity."	
		 );

// The annotation for "level" is too long, working on it.  

abstract subjective ordering Level 
      "Two-level hierarchy for generic discretizations. Classes are used by other orderings. The 'parent' keyword locates the parent concepts in the second-level hierarchy in case all children are used." 
      has children  
        (Low 
        "Lowest degrees or intensities in the subjective ordering level"	
      	   has children 
      	     (Minimal
			 "The most minimal degree or intensity, impossible to diminish"	      	  	
      	     ), 
      	     (ExtremelyLow
      	     "An exceptionally low degree or intensity"
      	     ), 
      	     (VeryLow
      	     "A markedly low degree or intensity"
      	     ), 
      	     (ModeratelyLow
      	     "A moderately low degree or intensity"
             )
         ), 
        (ModerateHigh 
        	"Intermediate and higher degrees or intensities in the subjective ordering level"
       	   has children
       	     (ModeratelyHigh
       	      "A moderately high degree or intensity"	
       	     ),
       	     (VeryHigh 
       	     "A significantly high degree or intensity"	
       	     ),
       	     (ExtremelyHigh
       	     "An exceptionally high degree or intensity"	
       	     ),       	     
       	     (Maximal
			 "The utmost degree or intensity, impossible to augment")
			 );

//Realm
	
abstract realm Realm
    "the physical context, such as a geographical or environmental domain, within which a concept exists or operates"
	equals core odo:Realm;

//Role

abstract role Role
    "Role specifies predicates that can be played by a concept in a context"
	equals core odo:Role;

role Reference
    "Applied to an observation for comparison. Can be linked to rescaling filters to adjust observations made across different contexts of space and time."
    inherits decision:Comparable;

role Indicator 
	"A measure or sign that reflects a particular state, condition, or trend within a defined context of observation"
   has children	
     (AbsoluteIndicator
     "Provides values within a known range, offering a clear understanding of the state it represents"	
       ),
     (RelativeIndicator 
     "Compares states of the same entity within the same context, without implying absolute values for the states being compared."
     );

/**
 * Observable
 */

//Process
abstract process Process
    "A dynamic occurrence unfolding over time, typically open-ended (no temporal boundaries) and homogeneous in nature."
	equals core odo:Process;

//Event
     
abstract event Event
    "An occurrence unfolding within a defined period in time, characterized by distinct temporal boundaries and involving participants."
	equals core observation:Event;
	 
	 
deniable attribute Positive
    "Describes the positive or negative outcome of an event or a subject."
    requires identity Goal;


abstract relationship Relationship
    "Directional connection between two subjects or agents"
	equals core odo:Relationship;
 
 
abstract configuration Configuration
    "represent emergent patterns that arise from observations of qualities and relationships, often manifesting as aggregates of other entities."
	equals core odo:Configuration;

//cancella: system
//Literature on systems: Laszlo, A., & Krippner, S. (1998). Systems theories: Their origins, foundations, and development. Advances in Psychology-Amsterdam-, 126, 47-76.
abstract configuration System
    "A set of interacting or interdependent components forming an integrated whole, unified by some form of regular interaction or interdependence."
    emerges from Relationship;
    
abstract configuration Network 
    is Configuration of Relationship
	"A network is a configuration of subjects connected by relationships."
	has children 
	    (abstract SpatialNetwork  
		inherits Areal
		),
		(
		abstract IntentionalNetwork
		requires identity Goal	
		);

//Redundant		
//abstract attribute ConfigurationType
//     "Describes configurations."
 //    applies to Configuration
 //    has children
  //       (PerceivedConfiguration
  //       inherits physical:Perceived),
  //       (InferredConfiguration
  //       	"An inferred configuration exists because humans can logically infer its existence based on other observations."	
 //            has children
 //            MeasuredConfiguration
 //            "A measured configuration exists because its existence is exposed by a measurement instrument or process."
 //            );

/**
 * Countable
 */

abstract agent Substantial
	"Independent agent which bears qualities. Similar to Substantial in UFO and DOLCE." 
	equals core odo:Substantial;

abstract thing Subject
    "Subject is a Substantial without agentive characteristics"
	equals core odo:Subject;

abstract agent Agent
	"Agent is a Substantial with intentionality" 
	equals core odo:Agent
	inherits decision:Intentional;
	
//DOLCE and UFO
abstract agent SocialAgent
    "Is a Substantial with intentionality, managed and recognized by other agents, such as organization."
    applies to Network
    contains at least 2 Agent;

//DOLCE and UFO    
abstract thing SocialObject
     "Subject acknowledged and interacted with by other agents, such as through legal frameworks and monetary systems."
	has children 
		(AgentiveSocialObject
			" Object or entity that actively participates in social interactions with intentionality or agency."
		),
   		(NonAgentiveSocialObject
   			"social objects that lack agency or intentionality in social interactions."
   		);
   		
/**
 * Qualities
 */
 
 //Comment: I would like to find a better and shorter definition for Quality. I'm searching for it. 
abstract quality Quality
     "Represent observables that cannot be observed directly but only through other entities that bear them, in this sense qualities inhere to other entities and are tied to their bearers"
     equals core odo:Quality;

abstract quantity Quantity
    "Quantities are qualities that can be quantified."     
	equals core odo:QuantifiableQuality;
	
abstract duration Duration 
	"The length of time during which an event or process occurs. It quantifies the temporal extent of an occurrence, measured from its beginning to its end."
	equals core odo:Duration;
	

//Space qualities
//Comment: re-think "observation" ontology and the concepts below
abstract area Area 
	equals core observation:Area 
	requires identity (Areal or Volumetric);


abstract length Length
	equals core observation:Length 
	requires identity (Lineal or Areal or Volumetric);
     	 

//Artifact capability inspired by: Borgo, S., Vieu, L.: Artefacts in formal ontology. In: Philosophy of technology and engineering sciences
abstract attribute Capability
     "Capability is the described quality of certain artifacts and natural agents."
     requires identity Goal
     describes Quality
     has disjoint children
        ArtifactCapability,
        NaturalCapability;
        
//abstract attribute AttributedCapability
//     "Attributed capability refers to capabilities of artifacts and natural agents assigned by other agent."
//     applies to physical:Artifact, physical:HumanMadeResource, Agent;


//Numerosity

abstract quantity Numerosity     
	"The count or quantity of discrete entities or events within a given set or context."	
	equals core odo:Numerosity;


/**
 * Intensive physical properties 
 */
 
abstract angle Angle 
	"Geometric property that measures the amount of rotation needed to bring one line or plane into coincidence with another. Typically measured in degrees or radians."
	equals core odo:Angle;

abstract energy Energy 
	"The capacity of a system to perform work or produce heat. Typically measured in joules (J)."
	equals core odo:Energy;

abstract temperature Temperature 
	"Measure of the average kinetic energy of the particles in a substance or system. Typically measured with scales such as Celsius, Fahrenheit, or Kelvin."
	equals core odo:Temperature;

resistivity Resistivity 
	"A measure of a material's resistance to the flow of electric current. It quantifies how strongly a material opposes the flow of electrical charges and is typically measured in ohm-meters (Ω⋅m)."
	equals core odo:Resistivity;

abstract velocity Velocity 
	"Quantity that describes the rate of change of an object's position with respect to time. Typically measured in units such as meters per second (m/s) or kilometers per hour (km/h)."
	equals core odo:Speed;


abstract mass Mass 
	"Property of matter that quantifies the amount of substance in an object. Typically measured in units such as kilograms (kg) or grams (g)."
	equals core observation:Mass 
	requires identity Volumetric;

abstract amount Amount
	"Refers to a quantitative measure or quantity of something. It represents the magnitude or volume of a substance, object, or phenomenon, and can be expressed numerically or qualitatively."
	equals core observation:Amount
	requires identity Volumetric;

abstract volume Volume 
	"Measure of the three-dimensional space occupied by an object or substance. It quantifies the amount of space enclosed by the boundaries of the object or substance and is typically measured in cubic units such as cubic meters (m³) or cubic centimeters (cm³)."
	equals core observation:Volume
	requires identity Volumetric;
	
abstract weight Weight  
	"The force exerted on an object due to gravity. Typically expressed in units of force such as newtons (N) or pounds-force (lbf)."
	equals core observation:Weight  
	requires identity Volumetric;
	
abstract quantity Proportion 
	"The relative size, quantity, or magnitude of one element compared to another within a given context. Can be expressed numerically, often as a fraction, percentage, or ratio."    
	equals core odo:Proportion;

abstract quantity Ratio  
	"The quantitative relationship between two quantities or amounts, typically expressed as the quotient of one quantity divided by another. It indicates the proportion or comparative magnitude of one quantity relative to another. Ratios can be represented in various forms, such as fractions, decimals, or percentages."   
	equals core odo:Ratio;

abstract volume Volumetric
	"Property or characteristic associated with volume or three-dimensional space. It pertains to the measurement or description of the amount of space occupied by an object or substance. Measurements may involve cubic units such as cubic meters, cubic centimetres, or cubic feet."
	equals core observation:Volumetric

//Category
	
abstract class Type 
    "Abstract classification defining categories of entities."        
	equals core odo:Type; 

//Priority

abstract priority Priority 
	"Relative importance or precedence assigned to entities or actions within a system or context, often based on predefined criteria or constraints."
	equals core odo:Priority;

abstract priority Concordance
"Conceptual distance from an idea or theory. Degree of agreement, harmony, or consistency between ideas, theories, or entities within a given context or domain."
;

//Value

abstract money MonetaryValue
	"Quantitative representation of the economic worth or exchangeable value assigned to goods, services, or assets, typically denoted in a currency unit."
    equals core odo:MonetaryValue;
    
abstract quantity PreferenceValue
	"Quantitative or qualitative measure representing the degree of desirability, favorability, or priority assigned to an option, choice, or outcome within a decision-making context."
   equals core odo:PreferenceValue;
   
abstract role ValueObject
	"An entity that embodies, represents, or holds value, which may include but is not limited to monetary, conceptual, or symbolic value."
     applies to Subject;
     
abstract relationship ValuationRelationship
	"Connection or association between entities or concepts that involves the assessment, determination, or assignment of value."
     links Agent to Subject;

deniable attribute Regularity
	"Denotes the state or quality of being consistent, orderly, or conforming to a pattern or set of rules, often implying predictability and absence of deviation or randomness."
     has disjoint children
         (Regular
         	"State or condition characterized by consistency, orderliness, or adherence to established patterns or rules."
         ),
         (Irregular
         	" Lack of orderliness, or deviation from established patterns or rules, often exhibiting unpredictability or variability in behaviour or occurrence."
         );
         
//Extent

extent Multiplicity 
	"Quantity or number of instances, occurrences, or elements within a given set, system, or context."
    is core odo:Multiplicity;
   
abstract attribute MeasurementRelated
	"Pertaining to entities, properties, or phenomena that are directly or indirectly connected with the process of measurement, including instruments, standards, protocols, and units of measurement."
      has children
//@colormap(colors=(red white green), midpoint=0)
      (Net
      	"Value or quantity after deductions or adjustments have been made"
      ), 
      (Gross
      	"Value or quantity before deductions or adjustments have been made"
      );
      
//Temporal extent
       	   
 abstract extent Time 
     is core observation:Time	
     has disjoint children 
     (RegularTime 
     "Regular time is observed as a grid of equal contiguous durations." 
     inherits imod:Regular),
     (IrregularTime 
      "Irregular time is observed as contiguous sequence of intervals of possibly different duration."
     inherits imod:Irregular
     );
    
    
//Spatial extent
 
 abstract extent Space 
    "Space in the IM worldview is 0, 1,2 or 3-dimensional, with implied traits that describe human perception and modalities of description proper of an Euclidean view of the world."
    is core observation:Space
    has disjoint children ( 
       PuntalSpace 
       inherits Puntal
       ),
       (LinealSpace
       	inherits Lineal
       ),
       (abstract ArealSpace 
       inherits Areal 
           has children 
               (GridSpace 	
               	inherits Regular 
                ),
                (VectorSpace
                inherits Irregular	
                )
                ),
       (abstract VolumetricSpace
       	inherits Volumetric
       	    has children
       	         (VoxelSpace
       	         inherits Regular	
       	         ),
       	         (PolyedricSpace
       	         inherits Irregular	
       	         )
       	   );
