namespace imod
	"The imod (Integrated Modelling) ontology specifies semantics used everywhere in the IM worldview
	 and links im-based concepts to the fundamental ontology for use in derived products."
	version 1.0
	in domain root
	metadata {
		dc:creator: (
			"Ferdinando Villa (ferdinando.villa@bc3research.org)"
			"Greta Adamo (greta.adamo@bc3research.org)"
		)
	};


/**
 * Import classes from ODO-IM core ontology: https://github.com/integratedmodelling/odo-im
 */	

/**
 * Predicate
 */
 
abstract identity Identity
    "Identity specifies the assigment of a concep to a certain class of concepts based on 
     the essential/identifying characterics of the concept itself"
	equals core observation:Identity;
	
abstract identity Goal
"Destination is the goal of a subject, agent or practice when created or internalized by another. Appropriate subclasses of this concept can be added or conferred any time a subject, agent or process has a goal."
   applies to Subject, Agent, Process
;


//Attribute
	
abstract attribute Attribute
    "Attribute specifies descriptive accidental predicates of a subject."
	equals core observation:Attribute;
	

	
attribute Predicted
	is core observation:Predicted;


//from GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Change	
// subclasses from Case file items of CMMN
abstract attribute Change
	"These attributes have to do with the comparison of a value along time. They are important
	 in dynamic models."
    applies to Quantity 
	has disjoint children
		  (Created 
			"Applied to a numeric quantity, it makes it a derivative. Dynamic models use this
			 automatically." 
			has disjoint children
				(Updated 
					"Describes the positive component of change. Implies quantifiable."
					applies to Quantity
				),
				(Deleted 
					"Describes the negative component of change. Implies quantifiable."
					applies to Quantity
				)
		),
		(Differential
			""
			has children 
			   	(Retained 
					"Describes the unchanging component of a changing quantity. Implies quantifiable."
					applies to Quantity
				)		
		)
;

deniable attribute Compliant 
"Compliance is forced adherence to a law, regulation, rule, process or practice"
;

deniable attribute Conformant 
"Conformance is voluntary adherence to a standard, rule, specification, requirement, design, process or practice."
;

abstract subjective ordering Coarseness 
	has children
		Fine,
		Coarse;
		
	
	
//Realm
	
abstract realm Realm
    "Realm specifies the physical (e.g. of the biosphere) location of a concept"
	equals core observation:Realm;

//Role

abstract role Role
    "Role specifies predicates that can be played by a concept in a context"
	equals core observation:Role;

//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Processual_role	
abstract role ProcessualRole
      "ProcessualRole refers to the role played by participants in a process."
      applies to Process;

//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Relational_role
abstract role RelationalRole
      "RelationalRole refers to the role played by participants in a functional or structural relationship."
      applies to Relationship
      ;
  

//Adamo, G., and Willis, M. (2022, May). Conceptual Integration for Social-Ecological Systems: An Ontological Approach. RCIS 2022
abstract role Resource
     "A resource is the role that objects plays in the context of activities or plans to achieve goals"
     applies to Subject, Agent
     has children
        (Natural),
        (HumanMade
        "HumanMade resources are the equivalent of artifacts."
        )
;

/**
 * Observable
 */

//Process
abstract process Process
    "A process is something happening in time, open-ended (without temporal boundaries), and homogeneous"
	equals core observation:Process;

abstract process Activity
    "An activity is a process performed by an agent"
    applies to Agent
    has children 
		(Creation
		),
		(Modification
		),
		(Cancellation 
			)  
;
	

abstract identity TransitiveProcess 
	"Transitive processes happen in a subject directly affecting other observables (their objects)"
	applies to Process
	has children
		(
			@example("Transportation: you transport 'something' - the object - modifying its spatial extent.")
			abstract SpatialTransitiveProcess
				"A spatial transitive process affects the spatial extent of the object."
				has children
			        (abstract Transport	
			          )
			);
	
abstract identity IntransitiveProcess 
	"Intransitive processes happen in subjects only affecting the context itself"
	applies to Process
	has children
		(abstract SpatialIntransitiveProcess
			"A spatial transitive process affects the spatial extent of the context - e.g. movement."
			has children
				(Movement
					""
				)
		);
abstract process Transformation
    "Any endogenous transformation happening in a system and affecting its internal structure."
    inherits IntransitiveProcess
//    affects Quality
    applies to System
    has children 
		(Coupling
			"Building of interactions. Can affect the mereological identity of the system by creating groups."
		),
		(Decoupling
			"Destruction of interactions. Can affect the mereological identity of the system by destroying groups."
			has children 
				(Dissipation
					"Proceeds through the collapse of parts of the system."
				)
		),
		(Displacement 
			"Akin to movement but causing the system to change because of that"
		),
		(Growth
			 "Positive or negative growth makes a system acquire a larger size or functional throughput."
		),
		(Homeostasis
			"The active maintenance of the current status and/or behavior of the system"
		)
;

//Event
     
abstract event Event
    "An event is something happening in time, close-ended (with temporal boundaries), having participants"
	equals core observation:Event;

event Collapse
   "The loss of identity for the system, after which it ceases to exist as such. In  living systems, it's synonymous with death."
    applies to System
;

//Relationship
	
abstract relationship Relationship
    "A relationship directionally connects two subjects or agents"
	equals core observation:Relationship;

	
//Tahko, Tuomas E. and E. Jonathan Lowe, "Ontological Dependence", The Stanford Encyclopedia of Philosophy (Fall 2020 Edition), Edward N. Zalta (ed.), URL = <https://plato.stanford.edu/archives/fall2020/entries/dependence-ontological/>.
//Adamo, G., Borgo, S., Di Francescomarino, C., Ghidini, C., Guarino, N., & Sanfilippo, E. M. (2018). Business process activity relationships: is there anything beyond arrows?. In Business Process Management Forum: BPM Forum 2018, Sydney, NSW, Australia, September 9-14, 2018, Proceedings 16 (pp. 53-70). Springer International Publishing.
abstract relationship Dependence
    "A dependence (ontological dependence) is a relationship that holds when an entity is dependent upon another for their existence"
     has children 
		(abstract HistoricalDependence
			"X is historically dependent from Y, iff the existence of X at time t is tied to the existence of Y at t' and t'< t"
		),
		(abstract CausalDependence
			"X is causally dependent on Y, iff the existence of X at t is explained by the existence of Y at t' and t'< t"
		),
		(abstract CooccurrenceDependence
			"X and Y are both necessary for the satisfaction of a goal"
		requires identity Goal	
		)
;  
 
	
abstract configuration Configuration
    "Configurations represent emergent patterns (aggregates of other entities) that are generated by observations of 
     quality and relationship"
	equals core observation:Configuration;

//Literature on systems: Laszlo, A., & Krippner, S. (1998). Systems theories: Their origins, foundations, and development. Advances in Psychology-Amsterdam-, 126, 47-76.
abstract configuration System
    "A system is a complex boundary entity that includes other entities, such as subjects, agents, events and relationships."
    emerges from Relationship
//    contains Subject, Agent, Event, Process, Relationship
;

/**
 * Countable
 */

abstract agent Substantial
	"Substantial is independent for its existence and bears qualities. Substantial are similar to Subtantial 
    in UFO and DOLCE" 
	equals core observation:Substantial 
;

abstract thing Subject
    "Subject is a Substantial without agentive characteristics"
	equals core observation:Subject
;

//Boundary literature: Varzi, Achille, "Boundary", The Stanford Encyclopedia of Philosophy (Winter 2015 Edition), Edward N. Zalta (ed.), URL = <https://plato.stanford.edu/archives/win2015/entries/boundary/>.
abstract thing Boundary 
    "A boundary is a spatial, temporal and conceptual delimitation between entities"
    inherits Lineal;

deniable attribute Intentional
	"Intention is a mental state (e.g. goal, belief, desire)";

abstract agent Agent
	"Agent is a Substantial with intentionality" 
	equals core observation:Agent
	inherits Intentional 
;
	

/**
 * Qualities
 */
 
abstract quality Quality
     "Qualities represent observables that cannot be observed directly but only through other entities that bear them, in this sense qualities inhere to other entities and are tied to their bearers"
     equals core observation:Qiality;

abstract quantity Quantity
    "Quantity are quality that can be quantified."     
	equals core observation:QuantifiableQuality;

//Definition of diversity: Xu, S., Bottcher, L., and Chou, T. (2020). Diversity in biology: definitions, quantification and models. Physical Biology, 17(3), 031001.
abstract quantity Diversity
"Diversity is the quantity of the range of characteristics found in subjects and agents."; 

abstract quantity Heterogeneity equals Diversity;

//Numerosity

abstract quantity Numerosity     
	equals core observation:Numerosity;

abstract quality Frequency
	"Frequency is the numerosity of events in a time period. Used in continuous time representations to 
    describe a wave."
	is count of Event;

/**
 * Intensive physical properties 
 */
 
abstract angle Angle 
	equals core observation:Angle;

abstract energy Energy 
	equals core observation:Energy;

abstract duration Duration 
	equals core observation:Duration;

abstract duration Age;
	
abstract duration Period
	"The time between two repetitions of an event. If the events are described by a quality in continuous time, it 
     describes the distance between high points on a wave function. Inversely proportional to frequency."
	decreases with Frequency;

abstract ordering Periodicity
	""
	describes Period
	has children
		(Sporadic 
			""
		),
		(Intermittent 
			""
		),
		(Continuous 
			""
		);

abstract temperature Temperature 
	equals core observation:Temperature;

resistivity Resistivity 
	equals core observation:Resistivity;

abstract velocity Velocity 
	equals core observation:Speed;

abstract quality Acceleration
	is change rate of Velocity;

abstract attribute MovementRelated
	"MovementRelated describe the type of velocity of a process"
	describes Velocity of Process
	has disjoint children
		Moving,
		Still;

quantity Impedance
	"The most general quality that describes resistance to a physical force, flow or 
     other phenomenon. Usually given the symbol Z in technical literature."
	// TODO children - electrical has a core representation
;

quantity Conductance
	"The opposite of impedance, describing the effectiveness of transport of an observable through a medium or a region.."
	// TODO children - fluid conductance, electrical conductance, thermal conductivity
;
 
/**
 * Extensive physical properties 
 */

abstract area Area 
	equals core observation:Area 
	requires identity (Areal or Volumetric);

abstract area SpatialCoverage is Area;

abstract length Length
	equals core observation:Length 
	requires identity (Lineal or Areal or Volumetric);

abstract mass Mass 
	equals core observation:Mass 
	requires identity Volumetric;

abstract amount Amount
	equals core observation:Amount
	requires identity Volumetric;

abstract volume Volume 
	equals core observation:Volume
	requires identity Volumetric;

abstract volume Capacity;
	
abstract weight Weight  
	equals core observation:Weight  
	requires identity Volumetric;

/**
 * Relative quantities 
 */
 
abstract quantity Proportion     
	equals core observation:Proportion;

abstract quantity Ratio     
	equals core observation:Ratio;
 

//Category
	
abstract class Type 
    "Type defines categories"        
	equals core observation:Type; 

//Priority

abstract priority Priority 
	equals core observation:Priority;

abstract priority Concordance
"Conceptual distance from an idea or theory"
;

//Value



