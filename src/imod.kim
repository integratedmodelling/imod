namespace imod
	"The imod (Integrated Modelling) ontology specifies semantics used everywhere in the IM worldview
	 and links im-based concepts to the fundamental ontology for use in derived products."
	version 1.0
	in domain root
	metadata {
		dc:creator: (
			"Ferdinando Villa (ferdinando.villa@bc3research.org)"
			"Greta Adamo (greta.adamo@bc3research.org)"
		)
	};


/**
 * Import classes from ODO-IM core ontology: https://github.com/integratedmodelling/odo-im
 */	

/**
 * Predicate
 */
 
abstract identity Identity
    "Identity specifies the assignment of a concept to a certain class of concepts based on 
     a series of characteristics, such as morphological, behavioral and contextual"
	equals core odo:Identity;
	
abstract identity Goal
"Destination is the goal of a subject, agent or practice when created or internalized by another. Appropriate subclasses of this concept can be added or conferred any time a subject, agent or process has a goal."
   applies to Subject, Agent, Process
;


//Attribute
	
abstract attribute Attribute
    "Attribute specifies descriptive accidental predicates of a subject."
	equals core odo:Attribute;
	

	
attribute Predicted
//	is core observation:Predicted
    applies to Quantity;




deniable attribute Compliant 
"Compliance is forced adherence to a law, regulation, rule, process or practice"
;

deniable attribute Conformant 
"Conformance is voluntary adherence to a standard, rule, specification, requirement, design, process or practice."
;

abstract subjective ordering Coarseness 
	has children
		Fine,
		Coarse;
	
deniable attribute Perceived
    "Describes an perceivable observable phenomena"
    has children
        Visible,
        Audible,
        Smellable,
        Palpable
;

deniable attribute Comparable
   "Describes the ability to rank an object over another one."
   applies to Concordance
;

	
abstract subjective ordering Level 
      "Two-level hierarchy for generic discretizations. Classes are used by other orderings. The 'parent' keyword locates the parent concepts in the second-level hierarchy in case all children are used." 
      has children  
        (Low 
      	   has children 
      	     Minimal, 
      	     ExtremelyLow, 
      	     VeryLow, 
      	     ModeratelyLow
             ), 
        Moderate, 
       (High 
       	   has children
       	     ModeratelyHigh,
       	     VeryHigh,
       	     ExtremelyHigh,
       	     Maximal
       	   )
;

abstract subjective ordering Severity 
     is Level;
     
abstract subjective ordering Relevance 
     "Describes the relevance of observables"
     is Level
     ;

	
//Realm
	
abstract realm Realm
    "Realm specifies the physical (e.g. of the biosphere) location of a concept"
	equals core odo:Realm;

//Role

abstract role Role
    "Role specifies predicates that can be played by a concept in a context"
	equals core odo:Role;


  

//Adamo, G., and Willis, M. (2022, May). Conceptual Integration for Social-Ecological Systems: An Ontological Approach. RCIS 2022
abstract role Resource
     "A resource is the role that objects plays in the context of activities or plans to achieve goals"
     applies to Subject, Agent
     has children
        (NaturalResource),
        (HumanMadeResource
        "HumanMade resources are the equivalent of artifacts."
        applies to Artifact
        );




role Reference
    "Applied to an observation used as reference to compute the primary observation by comparison. Can be linked to a rescaling filters to shift the context of observation made in different space and time."
    inherits Comparable
;

role Indicator 
   applies to Quantity 
   has children	
     (AbsoluteIndicator
     "An absolute indicator has values that range between a known minimum and maximum and the value corresponds to a state that is fully understood in terms of the indicator."	
       ),
     (RelativeIndicator 
     "A relative indicator can only compare two states of the same entity within the same context of observation, and does not suggest any absolute value for the state it refers to."
     );

abstract role Throughput 
   "The quantifiable amount of matter, energy or information transferred through a flow connection."
   applies to Process
  ;

/**
 * Observable
 */

//Process
abstract process Process
    "A process is something happening in time, open-ended (without temporal boundaries), and homogeneous"
	equals core odo:Process
;

abstract process Activity
    "An activity is a process performed by an agent"
    is Intentional Process
    applies to Agent
    ;

abstract process LifeCycle
    is Process 
    inherits Change
    applies to Subject, Agent;
    
process Assessment
    "The activity that represents observations made by the 'implicit' observer of the root context: the k.LAB user. Do not use without thinking hard first."
    is Activity 
    creates InformationRealization;


   
abstract subjective ordering Feasability
     "Describes the level of feasibility of an activity"
     applies to Activity
     has children
     Feasable,
     Unfeasible  
     ;
	

abstract identity TransitiveProcess 
	"Transitive processes happen in a subject directly affecting other observables (their objects)"
	applies to Process
	has children
		(
			@example("Transportation: you transport 'something' - the object - modifying its spatial extent.")
			abstract SpatialTransitiveProcess
				"A spatial transitive process affects the spatial extent of the object."
				has children
			        (abstract Transport	
			          )
			);
	
abstract identity IntransitiveProcess 
	"Intransitive processes happen in subjects only affecting the context itself"
	applies to Process
	has children
		(abstract SpatialIntransitiveProcess
			"A spatial transitive process affects the spatial extent of the context - e.g. movement."
			has children
				(Movement
					""
				)
		);
		
abstract process Transformation
    "Any endogenous transformation happening in a system and affecting its internal structure."
    inherits IntransitiveProcess, Change
    applies to System
    has children 
		(Coupling
			"Building of interactions. Can affect the mereological identity of the system by creating groups."
		),
		(Decoupling
			"Destruction of interactions. Can affect the mereological identity of the system by destroying groups."
			has children 
				(Dissipation
					"Proceeds through the collapse of parts of the system."
				)
		),
		(Displacement 
			"Akin to movement but causing the system to change because of that"
		),
		(Growth
			 "Positive or negative growth makes a system acquire a larger size or functional throughput."
		),
		(Homeostasis
			"The active maintenance of the current status and/or behavior of the system"
		)
;

//Event
     
abstract event Event
    "An event is something happening in time, close-ended (with temporal boundaries), having participants"
	equals core observation:Event;
	
abstract role Participant
     "Participants are entities, Subjects or Agents, that participates in Events."
     is Role during Event
;



//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Processual_role	
abstract role ProcessualRole
      "ProcessualRole refers to the role played by participants in a process."
      is Role during Process;

//From GFO: https://bioportal.bioontology.org/ontologies/GFO/?p=classes&conceptid=http%3A%2F%2Fwww.onto-med.de%2Fontologies%2Fgfo.owl%23Relational_role
abstract role RelationalRole
      "RelationalRole refers to the role played by participants in a functional or structural relationship.";

abstract event Input
    "Input is an event that triggers another event or process."
    applies to Event, Process
    inherits Change
;

abstract event Output
    "Output is an event generated from another event or process."
    applies to Event, Process
    inherits Change;

abstract process Participation
      is Activity
      applies to Participant
      has children
       Active,
       Passive    
      ;

abstract event RecurrentEvent 
     "These are intended not as units of measurement (not observables) but as observable events, signaling the expiration of a given period of time. They can be observe  in time as dependencies, and models can be provided for them in scenarios. Traits  can be used or conferred by processes to distinguish particular periods." 
      has disjoint children 
          (Second
			"The expiration of one second after a temporal point of reference."
		  ),
		  (Minute
			"The expiration of one minute after a temporal point of reference."			
		  ),
		  (Hour
			"The expiration of one hour after a temporal point of reference."		
		   inherits Hourly	
	      ),
		  (Day
			"The expiration of one day after a temporal point of reference."
		  inherits Daily			
		  ),
		  (Week
		  inherits Weekly
		  has children
		  Monday,
		  Tuesday,
		  Wednesday,
		  Thursday,
		  Friday,
		  Saturday,
		  Sunday	
		  ),
	     (Month	
	     "The expiration of one month after a temporal point of reference." 
	     inherits Monthly
         has children 
         January,
         February,
         March,
         April,
         May,
         June,
         July,
         August,
         September,
         October,
         November,
         December
         ),
        (Season
		 "The expiration of one season after a temporal point of reference."			
		),
		(Year
		"The expiration of one year after a temporal point of reference."			
		inherits Yearly
		),
		(Decade
		"The expiration of ten years after a temporal point of reference."
		),
		(Century
		"The expiration of hundred years after a temporal point of reference."	
		),
		(Millenium
		"The expiration of one thousand years after a temporal point of reference."
		)
		;	

//From DOLCE ontology
abstract event Achievement
      "Atomic culminating event"
;

abstract attribute AchievementRelated 
        "The type of achievement or non-chievement related to an outcome." 
            applies to Achievement
            has children 
            Realized, 
            Potential,
            Theoretical;

event Collapse
   "The loss of identity for the system, after which it ceases to exist as such. In  living systems, it's synonymous with death."
    applies to System
;

deniable attribute Planned
    "Describes planned or unplanned events"
    applies to Event;
    
deniable attribute Positive
    "Describes the positive or negative outcome of an event or a subject"
    applies to Event, Achievement, Subject
;

//Relationship
	
abstract relationship Relationship
    "A relationship directionally connects two subjects or agents"
	equals core odo:Relationship;

	
//Tahko, Tuomas E. and E. Jonathan Lowe, "Ontological Dependence", The Stanford Encyclopedia of Philosophy (Fall 2020 Edition), Edward N. Zalta (ed.), URL = <https://plato.stanford.edu/archives/fall2020/entries/dependence-ontological/>.
//Adamo, G., Borgo, S., Di Francescomarino, C., Ghidini, C., Guarino, N., & Sanfilippo, E. M. (2018). Business process activity relationships: is there anything beyond arrows?. In Business Process Management Forum: BPM Forum 2018, Sydney, NSW, Australia, September 9-14, 2018, Proceedings 16 (pp. 53-70). Springer International Publishing.
abstract relationship Dependence
    "A dependence (ontological dependence) is a relationship that holds when an entity is dependent upon another for their existence"
     has children 
		(abstract HistoricalDependence
			"X is historically dependent from Y, iff the existence of X at time t is tied to the existence of Y at t' and t'< t"
		),
		(abstract CausalDependence
			"X is causally dependent on Y, iff the existence of X at t is explained by the existence of Y at t' and t'< t"
            applies to Event, Process
		),
		(abstract CooccurrenceDependence
			"X and Y are both necessary for the satisfaction of a goal"
		requires identity Goal	
		)
;  
 
	
abstract configuration Configuration
    "Configurations represent emergent patterns (aggregates of other entities) that are generated by observations of 
     quality and relationship"
	equals core odo:Configuration;

//Literature on systems: Laszlo, A., & Krippner, S. (1998). Systems theories: Their origins, foundations, and development. Advances in Psychology-Amsterdam-, 126, 47-76.
abstract configuration System
    "A system is a complex boundary entity that includes other entities, such as subjects, agents, events and relationships."
    emerges from Relationship;
    
abstract configuration Network 
    is Configuration of Relationship
	"A network is a configuration of subjects connected by relationships."
	has children 
	    (abstract SpatialNetwork  
		inherits Areal
		);
		
abstract attribute DescribedConfiguration
     "Describes configurations."
     applies to Configuration
     has children
         (PerceivedConfiguration
         inherits Perceived),
         (InferredConfiguration
         	"An inferred configuration exists because humans can logically infer its existence based on other observations."	
             has children
             MeasuredConfiguration
             "A measured configuration exists because its existence is exposed by a measurement instrument or process."
             );

/**
 * Countable
 */

abstract agent Substantial
	"Substantial is independent for its existence and bears qualities. Substantial are similar to Subtantial 
    in UFO and DOLCE" 
	equals core odo:Substantial 
;

abstract thing Subject
    "Subject is a Substantial without agentive characteristics"
	equals core odo:Subject
;

abstract thing Artifact
    "Artifact s intentionally created by an agent for a goal"
    requires identity Goal
;

//From: Borgo, S., Vieu, L.: Artefacts in formal ontology. In: Philosophy of technology and engineering sciences
abstract attribute Capability
     "Capability is a quality of a certain artifact."
     applies to Artifact, HumanMadeResource
;

abstract attribute AttributedCapability
     "Attributed capability refers to capacities of artifacts assigned by an agent."
     applies to Artifact, HumanMadeResource
;


deniable attribute Intentional
	"Intention is a mental state (e.g. goal, belief, desire)";

abstract agent Agent
	"Agent is a Substantial with intentionality" 
	equals core odo:Agent
	inherits Intentional 
;

// From: DOLCE Lite Plus (DLP). E. M. Sanfilippo: "Ontologies for information entities: State of the art and open challenges"
abstract thing InformationRealization
     "Information realization is the physical support of an information object"
     has children
        (Document
            has children
                (Specification),
                (PlanRealization),
                (Book),
                (Report)
                ),
                
        (ComputerProgram)
        ;

abstract attribute Evidence
     "Characterizes degrees of evidence for an hypothesis." 
     has children 
         (Possible 
         "Hypothesis cannot be dismissed based on reasoning although no documented supporting observations exist."
         ),
         (Certain 
         "No doubt can remain about evidence - self-evident or blatant truth"
         ),
         (Hypothetical 
         "Hypothesis can be made in support of an interpretation although no documented supporting observations exist. Similarly valid counter-arguments may exist."
         ),
         (Observed
          "Degree of evidence is supported by documented observations, but still  evidence is indirect."
          )
;



/**
 * Qualities
 */
 
abstract quality Quality
     "Qualities represent observables that cannot be observed directly but only through other entities that bear them, in this sense qualities inhere to other entities and are tied to their bearers"
     equals core odo:Qiality;

abstract quantity Quantity
    "Quantity are quality that can be quantified."     
	equals core odo:QuantifiableQuality;

//Definition of diversity: Xu, S., Bottcher, L., and Chou, T. (2020). Diversity in biology: definitions, quantification and models. Physical Biology, 17(3), 031001.
abstract quantity Diversity
"Diversity is the quantity of the range of characteristics found in subjects and agents."; 

abstract quantity Heterogeneity equals Diversity;

//Numerosity

abstract quantity Numerosity     
	equals core odo:Numerosity;


/**
 * Intensive physical properties 
 */
 
abstract angle Angle 
	equals core odo:Angle;

abstract energy Energy 
	equals core odo:Energy;

abstract temperature Temperature 
	equals core odo:Temperature;

resistivity Resistivity 
	equals core odo:Resistivity;

abstract velocity Velocity 
	equals core odo:Speed;

abstract quality Acceleration
	is change rate of Velocity;

abstract attribute MovementRelated
	"MovementRelated describe the type of velocity of a process"
	describes Velocity of Process
	has disjoint children
		Moving,
		Still;

quantity Impedance
	"The most general quality that describes resistance to a physical force, flow or 
     other phenomenon. Usually given the symbol Z in technical literature."
	// TODO children - electrical has a core representation
;

quantity Conductance
	"The opposite of impedance, describing the effectiveness of transport of an observable through a medium or a region.."
	// TODO children - fluid conductance, electrical conductance, thermal conductivity
;
 
/**
 * Extensive physical properties 
 */

abstract mass Mass 
	equals core observation:Mass 
	requires identity Volumetric;

abstract amount Amount
	equals core observation:Amount
	requires identity Volumetric;

abstract volume Volume 
	equals core observation:Volume
	requires identity Volumetric;

abstract volume Capacity;

abstract angle Direction;
	
abstract weight Weight  
	equals core observation:Weight  
	requires identity Volumetric;
	

/**
 * Relative quantities 
 */
 
abstract quantity Proportion     
	equals core odo:Proportion;

abstract quantity Ratio     
	equals core odo:Ratio;
 

//Category
	
abstract class Type 
    "Type defines categories"        
	equals core odo:Type; 

//Priority

abstract priority Priority 
	equals core odo:Priority;

abstract priority Concordance
"Conceptual distance from an idea or theory"
;

//Value

abstract money MonetaryValue
    equals core odo:MonetaryValue;
    
abstract quantity PreferenceValue
   equals core odo:PreferenceValue;
   
abstract role ValueObject
     applies to Subject;

abstract role ValueActivity
     applies to Activity;
     
abstract relationship ValuationRelationship
     links Agent to Subject;

//Extent

extent Multiplicity 
    is core odo:Multiplicity;
    
abstract attribute MeasurementRelated
      has children
      (@colormap(colors=(red white green), midpoint=0)
      Net
      ), 
      Gross
      ;

//Used to annotate concepts
      
abstract attribute Appropriateness  
     has children 
     Appropriate, 
     Inappropriate;






